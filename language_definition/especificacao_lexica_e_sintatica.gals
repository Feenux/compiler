#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = gals
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
linha: "/" "/".*
#Tokens
//id
identificador: _([A-Z]|((([A-Z][a-z0-9]+)|([a-z]+[0-9]*))+[A-Z]?))

//palavra

reservada: [a-z]+([A-Z][a-z]+)* 
and = reservada: "and"
array = reservada: "array"
do = reservada: "do"
exit = reservada: "exit"
false = reservada: "false"
if = reservada: "if"
ifFalseDo = reservada: "ifFalseDo"
ifTrueDo = reservada: "ifTrueDo"
literal = reservada: "literal"
logical = reservada: "logical"
number= reservada: "number"
not = reservada: "not"
or = reservada: "or"
read = reservada: "read"
true = reservada: "true"
write = reservada: "write"

//constante numerica

constante_numerica: [0-9]+(\.[0-9]+)?

//constante caractere

constante_caractere: '(\\n | \\t | \\s)'

//constante literal

constante_literal:  \"[^ \" \n \\]*\"

//comentario de linha - olhar na especificacao como ignorar comentário


//simbolo especial

":"
","
"["
"]"
"{"
"}"
"("
")"
":="
"+"
"-"
"*"
"/"
"="
"!="
"<"
"<="
">"
">="

//ignorar caractere de formataÃ§Ã£o
:[\t\n\s]
:{linha}
#NonTerminals
<program>
<declaracao_variaveis_opt>
<declaracao_variaveis>
<declaracao_variaveis_1>
<variavel>
<tipo>
<tipo_simples>
<array>
<lista_identificadores>
<lista_identificadores_1>
<lista_comandos_opt>
<lista_comandos>
<lista_comandos_1>
<comando>
<atribuicao>
<identificador_complexo>
<acesso_array>
<entrada_dados>
<lista_identificadores_complexos>
<lista_identificadores_complexos_1>
<saida_dados>
<lista_expressoes>
<lista_expressoes_1>
<selecao>
<selecao_false>
<repeticao>
<expressao>
<expressao_1>
<elemento>
<relacional>
<relacional_1>
<operador_relacional>
<aritmetica>
<aritmetica_1>
<termo>
<termo_1>
<fator>
<fator_1>
#Grammar
<program> ::= #15 <declaracao_variaveis_opt> "{" <lista_comandos_opt> "}" #16;
<declaracao_variaveis_opt> ::= î | <declaracao_variaveis>;
<declaracao_variaveis> ::= <variavel> <declaracao_variaveis_1>;
<declaracao_variaveis_1> ::= î | <declaracao_variaveis>;
<variavel> ::= <tipo>#101 ":" <lista_identificadores> #103;
<tipo> ::= <tipo_simples> | <array>;
<tipo_simples> ::= number | logical | literal;
<array> ::= array "[" <tipo_simples> "," constante_numerica "]";
<lista_identificadores> ::= identificador #102 <lista_identificadores_1>;
<lista_identificadores_1> ::= î | "," <lista_identificadores>;
<lista_comandos_opt> ::= î | <lista_comandos>;
<lista_comandos> ::= <comando> <lista_comandos_1>;
<lista_comandos_1> ::= î | <lista_comandos>;
<comando> ::= <atribuicao> | <entrada_dados> | <saida_dados> | <selecao> | <repeticao>;
<atribuicao> ::= <identificador_complexo> ":=" <expressao> #106;
<identificador_complexo> ::= identificador #102 <acesso_array>;
<acesso_array> ::= î | "[" <expressao> "]";
<entrada_dados> ::= read "(" <lista_identificadores_complexos> #104 ")";
<lista_identificadores_complexos> ::= <identificador_complexo> <lista_identificadores_complexos_1>;
<lista_identificadores_complexos_1> ::= î | "," <lista_identificadores_complexos>;
<saida_dados> ::= write "(" <lista_expressoes> ")";
<lista_expressoes> ::= <expressao> #14 <lista_expressoes_1>;
<lista_expressoes_1> ::= î | "," <lista_expressoes>;
<selecao> ::= "(" <expressao> #107 ")" ifTrueDo "{" <lista_comandos_opt> "}" <selecao_false> #108;
<selecao_false> ::= î | #109 ifFalseDo "{" <lista_comandos_opt> "}";
<repeticao> ::= #110 do "{" <lista_comandos_opt> if "(" <expressao> #111 ")" exit <lista_comandos_opt> "}" #112;
<expressao> ::= <elemento> <expressao_1>;
<expressao_1> ::= î | and <elemento> #17 <expressao_1> | or <elemento> #18 <expressao_1>;
<elemento> ::= <relacional> | true #11 | false #12 | not <elemento> #13;
<relacional> ::= <aritmetica> <relacional_1>;
<relacional_1> ::= î | <operador_relacional> #9 <aritmetica> #10;
<operador_relacional> ::= "=" | "!=" | "<" | "<=" | ">" | ">=";
<aritmetica> ::= <termo> <aritmetica_1>;
<aritmetica_1> ::= î | "+" <termo> #1 <aritmetica_1> | "-" <termo> #2 <aritmetica_1>;
<termo> ::= <fator> <termo_1>;
<termo_1> ::= î | "*" <fator> #3 <termo_1> | "/" <fator> #4 <termo_1>;
<fator> ::= identificador #105 <fator_1>  | constante_numerica #6 | constante_caractere #19 | constante_literal #20 | "(" <expressao> ")" | "+" <fator> #7 | "-" <fator> #8;
<fator_1> ::= î | "[" <expressao> "]";

